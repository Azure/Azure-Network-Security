let TimeWindow   = 90d;    // How far back to look
let HitThreshold = 10;     // Minimum hits to alert per SourceIp + Category
let MinSeverity  = 1;      // Set Minimum Severity
let EnableCategoryFilter    = true;  // Filter 1: Change to true for filtering based on Categoriesofinterest defined below
let EnableDescriptionFilter = false;  // Filter 2: Change to true for filtering based on Descriptionsofinterest defined below
let EnableActionFilter      = false;  // Filter 3: Change to true for filtering based on a MatchActions defined below
let CategoriesOfInterest    = dynamic(["Attempted User Privilege Gain", "Unsuccessful User Privilege Gain", "Successful User Privilege Gain", "Attempted Administrator Privilege Gain", "Successful Administrator Privilege Gain"]); // Filter 1
let DescriptionsOfInterest  = dynamic(["attempted-user", "unsuccessful-user", "successful-user", "attempted-admin", "successful-admin"]); // Filter 2
let MatchActions            = dynamic(["Deny", "alert"]);   // Filter 3 
AZFWIdpsSignature
| where TimeGenerated >= ago(TimeWindow)
| where Severity >= MinSeverity
// Filter 1: Category filter (optional)
| where (EnableCategoryFilter == false) or (Category has_any (CategoriesOfInterest))
// Filter 2: Description filter (optional)
| where (EnableDescriptionFilter == false) or (Description has_any (DescriptionsOfInterest))
// Filter 3: Action filter (optional)
| where (EnableActionFilter == false) or (Action in~ (MatchActions))
| summarize
    StartTime = min(TimeGenerated),
    EndTime = max(TimeGenerated),
    TotalHits = count(),
    MaxSeverity  = max(Severity),
    Actions = make_set(Action, 5),
    Signatures   = make_set(SignatureId, 20),
    Description  = make_set(substring(tostring(Description), 0, 120), 3)
    by SourceIp, ThreatCategory = Category
| where TotalHits >= HitThreshold
| project
    StartTime,
    EndTime,
    SourceIp,
    ThreatCategory,
    TotalHits,
    MaxSeverity,
    Actions,
    Signatures,
    Description
| order by MaxSeverity desc, TotalHits desc
