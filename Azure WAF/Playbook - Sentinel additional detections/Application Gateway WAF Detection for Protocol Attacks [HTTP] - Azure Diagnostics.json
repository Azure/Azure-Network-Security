let Threshold = 3;
AzureDiagnostics
| where Category == "ApplicationGatewayFirewallLog"
| where action_s == "Matched"
| where Message has_any ("HTTP Header Injection", "HTTP Response Splitting", "HTTP Splitting", "LDAP Injection")
| where ruleId_s startswith "921"
| where ruleGroup_s startswith "PROTOCOL-ATTACK"
| project transactionId_g, hostname_s, requestUri_s, TimeGenerated, clientIp_s, Message, details_message_s, details_data_s
| join kind = inner(
    AzureDiagnostics
    | where Category =~ "ApplicationGatewayFirewallLog"
    | where action_s =~ "Blocked"
) on transactionId_g
| summarize 
    StartTime = min(TimeGenerated),
    EndTime = max(TimeGenerated),
    TransactionID = make_set(transactionId_g, 100),
    Message = make_set(Message, 100),
    Detail_Message = make_set(details_message_s, 100),
    Detail_Data = make_set(details_data_s, 100),
    Total_TransactionId = dcount(transactionId_g)
  by clientIp_s, action_s
| where Total_TransactionId >= Threshold